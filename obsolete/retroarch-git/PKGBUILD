# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>
# Contributor: Themaister    <maister@archlinux.us>

# Modifier gripped <forum.odroid.com>  Note fails to build with GCC 5.2.0

pkgname=retroarch-git
pkgver=v1.2.2.r1054.g08c0cf1
pkgrel=1
epoch=1
pkgdesc="Reference frontend for the libretro API."
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="http://github.com/libretro/RetroArch"
license=('GPL')
# optional build-time deps: 'openal', 'libxml2', 'nvidia-cg-toolkit', 'sdl_image'
# see: 'retroarch --features' for more.
depends=('jack2' 'libgl' 'libusb' 'libxinerama' 'libxkbcommon'
	 'libxv' 'mesa' 'openal' 'python')
optdepends=('libretro-super-git: A collection of many libretro implementations.')
makedepends=('git')
provides=('retroarch')
conflicts=('retroarch')

_gitname=RetroArch
source=("git+https://github.com/libretro/${_gitname}.git"
        'arm.patch')
md5sums=('SKIP'
         '242488607dbb5d36b6d8e3b2584d2b5d')

pkgver() {
  cd "${_gitname}"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cd "${_gitname}"
  patch -p0 -i ../arm.patch
  ./configure --prefix=/usr --enable-udev --enable-threads --disable-ffmpeg --enable-gles --disable-kms \
  --disable-vg --enable-pulse --disable-xvideo --disable-sdl --enable-gles3 \
  --enable-fbo --enable-neon --disable-sdl2 --enable-floathard
  make
}

package() {
  cd "${_gitname}"
  make install DESTDIR="${pkgdir}"
  install -m755 "${srcdir}/RetroArch/tools/retroarch-joyconfig" "${pkgdir}/usr/bin/retroarch-joyconfig"
}
