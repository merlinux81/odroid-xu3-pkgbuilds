# Maintainer: Vlad M. <vlad@archlinux.net>
# Contributor: Attila Bukor <r1pp3rj4ck [at] w4it [dot] eu>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: xantares <xantares09@hotmail.com>
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Ricardo Band <me [at] xengi [dot] de>

# Modifier: gripped forum.odroid.com (credit to Leonard for the build instructions and patched files)

pkgname=popcorntime-armv7
_pkgname=popcorntime
pkgver=0.3.8.5
_pkgver=0.3.8-5
pkgrel=3
pkgdesc="Stream movies from torrents. Skip the downloads. Launch, click, watch."
arch=('armv7')
url="http://popcorntime.io/"
license=('GPL3')
makedepends=('git'
             'nodejs-bower' # install from AUR
             'nodejs-grunt-cli' # install from AUR
             'npm')
depends=('alsa-lib'
         'gconf'
         'gtk2'
         'libnotify'
         'libxtst'
         'nss'
         'ttf-font')
optdepends=('net-tools: vpn.ht client'
            'kodi-xu3: For use as external player set output port to 51234')
options=('!strip')
install="popcorntime.install"
conflicts=('popcorntime-xu3')
replaces=('popcorntime-xu3')
_commit_hash="9d0b6efe81f5a0a005e244115a94139a62a76459"
_gitname="desktop-v${_pkgver}-${_commit_hash}"
_nw_ver=0.12.2

_platform=linuxarm
_nw_platform=linux-arm

source=("desktop-v${_pkgver}.tar.bz2::https://git.popcorntime.io/popcorntime/desktop/repository/archive.tar.bz2?ref=v${_pkgver}"
        "popcorntime.desktop"
        'arm.patch'
        'kodi.patch'
        'stream.strm'
        'external-kodi-icon.png')
sha256sums=('b4980fc2ba7ffd53fbf7463b09d39a9f14a8b138708e3fb7f36a0939a3184190'
            '86d4e736d9fff29fad9892396d1b141c8768db3b80c529391d7b79f4c28c3126'
            '22e553e30a9cfb125f2dffb875b5134480865ba45639faf45376e536526fb7bf'
            '4e289c57ac57b975d438ae5a5e7094e5ce20303cb0fe92ab5ca7aaf16cd68f62'
            'c3ec78add813aa509e5dc231e04138ac83b2112ea565a40c7506c4de533fc8e4'
            'b37479efe014e4c23e603d1361bb039471e5fdfd24dba6c10078d2393e014fa7')
prepare() {
  cd "${srcdir}/${_gitname}"


  export PYTHON=/usr/bin/python2

  npm install
  
  patch --ignore-whitespace -p1 -i ../arm.patch
  patch --ignore-whitespace -p1 -i ../kodi.patch  #enable kodi as external player
  
  cp -v "${srcdir}"/external-kodi-icon.png src/app/images/icons/external-kodi-icon.png

}

build() {
  cd "${srcdir}/${_gitname}"

  grunt css
  grunt bower_clean
  grunt nodewebkit || grunt nodewebkit
}

package() {
  cd "${srcdir}"

  _bpath="${srcdir}/${_gitname}/build/releases/Popcorn-Time/${_platform}/Popcorn-Time"

  install -dm755 "${pkgdir}/usr/lib/${_pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  install -Dm755 "${_bpath}/Popcorn-Time" "${pkgdir}/usr/lib/${_pkgname}/"
  install -Dm644 "${_bpath}/nw.pak" "${pkgdir}/usr/lib/${_pkgname}/"
  install -Dm644 "${_bpath}/libffmpegsumo.so" "${pkgdir}/usr/lib/${_pkgname}/"
  install -Dm644 "${srcdir}/${_gitname}/build/cache/${_platform}/${_nw_ver}/icudtl.dat" "${pkgdir}/usr/lib/${_pkgname}/"
  install -Dm644 "stream.strm" "${pkgdir}/usr/lib/${_pkgname}/"

  mkdir -p "${pkgdir}/usr/bin"
  ln -s "/usr/lib/${_pkgname}/Popcorn-Time" "${pkgdir}/usr/bin/${_pkgname}"

  install -Dm644 "${srcdir}/popcorntime.desktop" "${pkgdir}/usr/share/applications/popcorntime.desktop"

  install -Dm644 "${srcdir}/${_gitname}/src/app/images/icon.png" "${pkgdir}/usr/share/pixmaps/popcorntime.png"
}