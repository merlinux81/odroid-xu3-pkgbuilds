# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Daniel Wallace <daniel.wallace@gatech.edu>

pkgname=plex-home-theater-xu3
_pkgver=1.4.0.459-b41b1814
pkgver=${_pkgver%-*}
pkgrel=2
pkgdesc='Plex Home Theater'
arch=('i686' 'x86_64')
url='http://www.plexapp.com/'
license=('GPL2')
depends=('boost-libs' 'curl' 'ffmpeg-compat' 'glew' 'libcdio' 'libmad'
         'libmicrohttpd' 'libmpeg2' 'libsamplerate' 'libssh' 'libusb-compat'
         'libvdpau' 'libxrandr' 'lzo' 'sdl_image' 'sdl_mixer' 'smbclient'
         'taglib' 'tinyxml' 'yajl')
makedepends=('boost' 'cmake' 'doxygen' 'git' 'java-environment' 'libcec'
             'libplist' 'libshairport' 'nasm' 'swig' 'unzip' 'zip')
optdepends=('libplist: AirPlay support'
            'libshairport: AirPlay support'
            'libcec: Pulse-Eight USB-CEC adapter support'
            'pulseaudio: PulseAudio support')
source=("https://github.com/plexinc/plex-home-theater-public/archive/v${_pkgver}.tar.gz"
        'plex-ffmpeg-compat.patch'
        'plex-libcec-2.2.0.patch'
        'plexhometheater.sh'
        'arm.patch'
        'mfc.patch')
sha256sums=('cc6485100afa0f7a4d483e05eeafba72764f55749bb3f3ee0a529646e076bbe4'
            '1ae6e5e744d7979b6ae5c516f2d145ac8ee06e0babb26a6e57e6143c1b16bfc3'
            'fb4bb1a5736d189e7a2137f39f6787bcae0e2c38073f9ddca96fb463fa50da2c'
            '491fbe63ab57a003a2d0cd39d6ed14636583cefb08f2a94b2a06b86318fb4dac'
            'fc9c94831cc43ff597ed129bcd2fb714cfeda218be7f0f675fdc23d9b7521bb9'
            '7599d8bbde9ae38c622b7ad7ffdd06eb22c460bff84b4104a01488fb52d8ff4b')

prepare() {
  cd plex-home-theater-public-${_pkgver}

  patch -Np1 -i ../plex-libcec-2.2.0.patch
  patch -Np1 -i ../plex-ffmpeg-compat.patch
  patch -p0 < ../arm.patch
  patch -p1 < ../mfc.patch
}

build() {
  cd plex-home-theater-public-${_pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' \
           -DCMAKE_C_FLAGS="$CMAKE_C_FLAGS -I/usr/include/glib-2.0 -I/usr/lib/glib-2.0/include" \
           -DCMAKE_CXX_FLAGS="$CMAKE_CXX_FLAGS -I/usr/include/glib-2.0 -I/usr/lib/glib-2.0/include" \
           -DCREATE_BUNDLE='FALSE' \
           -DDUMP_SYMBOLS='FALSE' \
           -DENABLE_AUTOUPDATE='FALSE' \
           -DENABLE_PYTHON='TRUE' \
           -DPYTHON_EXEC='/usr/bin/python2' \
           -DUSE_INTERNAL_FFMPEG='FALSE' \
           -DCMAKE_BUILD_TYPE='Release'

  make
}

package() {
  cd plex-home-theater-public-${_pkgver}/build

  make DESTDIR="${pkgdir}" install
  install -dm 755 "${pkgdir}"/usr/{lib/plexhometheater,share/{applications,pixmaps}}
  mv "${pkgdir}"/usr/bin/{system,xbmc-xrandr} "${pkgdir}"/usr/lib/plexhometheater/
  mv "${pkgdir}"/usr/share/XBMC "${pkgdir}"/usr/share/plexhometheater
  rm -rf "${pkgdir}"/usr/bin/*.so
  install -m 755 "${srcdir}"/plexhometheater.sh "${pkgdir}"/usr/bin/
  install -m 644 ../plex/Resources/plexhometheater.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 ../plex/Resources/plex-icon-256.png "${pkgdir}"/usr/share/pixmaps/plexhometheater.png
}

# vim: ts=2 sw=2 et:
