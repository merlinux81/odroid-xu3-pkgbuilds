# $Id: PKGBUILD 149273 2012-02-06 17:33:27Z ibiru $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Bart??omiej Piotrowski <bpiotrowski@archlinux.org>

# Modifier; gripped - forum.odroid.com

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building
#  - disable neon

# gripped added to patch to prevent -march and -mtune beieng passed to gcc

pkgname=libvpx-neon
_pkgname=libvpx
pkgver=1.5.0
pkgrel=2
pkgdesc='VP8 and VP9 codec'
arch=('i686' 'x86_64')
url='http://www.webmproject.org/'
license=('BSD')
depends=('glibc' 'gcc-libs')
makedepends=('yasm' 'git')
conflicts=('libvpx')
provides=("$_pkgname=${pkgver}" 'libvpx.so')
source=($_pkgname-$pkgver.tar.gz::https://github.com/webmproject/libvpx/archive/v$pkgver.tar.gz
        fix-armhf-link.patch
        mcpu.patch)
md5sums=('0c662bc7525afe281badb3175140d35c'
         'c772c77406ba346b954f139f65ae8e59'
         'f8e698c817e9607cbb47ee5c00040622')

prepare() {
  cd $_pkgname-$pkgver
  patch -p1 -i ../fix-armhf-link.patch
  patch -p0 -i ../mcpu.patch
}

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr \
    --enable-vp8 \
    --enable-vp9 \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --enable-postproc \
    --enable-pic \
    --enable-experimental --enable-spatial-svc \
    --disable-install-docs \
    --disable-install-srcs 
  make
}

package() {
  cd $_pkgname-$pkgver
  make DIST_DIR="$pkgdir/usr" install
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
