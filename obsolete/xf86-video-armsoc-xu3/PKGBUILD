# ArmSoc (OMAP + Mali)  FrameBuffer driver for Xorg from Linaro.
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Erik Andersson <erik.h.andersson@gmail.com>
# Modifier:   gripped forum.odroid.com

# Official upstream is at 
# http://git.linaro.org/gitweb?p=arm/xorg/driver/xf86-video-armsoc.git;a=summary

buildarch=4
_commit=ddd97ea4911f475fcb6cdc7ab1c1398bcc829566
pkgname=xf86-video-armsoc-xu3
_pkgname=xf86-video-armsoc
pkgdesc="armsoc framebuffer driver for X which attempts to comprimise between speed and usability on the xu3/4"
pkgver=238.ddd97ea
pkgrel=2
arch=('armv7h')
install=xf86-video-armsoc-xu3.install
url="git://anongit.freedesktop.org/xorg/driver/xf86-video-armsoc"
license=('GPL2')
makedepends=('git' 'pkgconfig' 'xorg-server-devel' 'resourceproto' 'scrnsaverproto')
options=('!libtool')
conflicts=('xf86-video-armsoc' 'xf86-video-armsoc-git' 'xf86-video-armsoc-odroid'
           'xf86-video-armsoc-dsd' 'xf86-video-armsoc-xu3-experimental')
replaces=('xf86-video-armsoc-dsd')
source=("git+http://anongit.freedesktop.org/git/xorg/driver/xf86-video-armsoc.git#commit=${_commit}"
	'xorg117.patch'
        'write_combine_caching.patch')
md5sums=('SKIP'
         'bf464fd87aab2e356b8e75ead4b5298c'
         'c6b22faad840f391120a92ef38679a58')
pkgver() {
  cd "$srcdir/$_pkgname"
  echo $(git rev-list --count makepkg).$(git rev-parse --short makepkg)
}

prepare() {
  cd "$srcdir/$_pkgname"
  patch -N -p0 < ../xorg117.patch
  patch -N -p0 < ../write_combine_caching.patch
}
	
build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr --with-drmmode=exynos
  make
}

package() {
  make -C "$srcdir/$_pkgname" DESTDIR="${pkgdir}" install
}
