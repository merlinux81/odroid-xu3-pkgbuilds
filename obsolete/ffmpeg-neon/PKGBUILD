# $Id: PKGBUILD 198639 2013-10-31 16:28:32Z bpiotrowski $
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Maintainer:  Bart??omiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Paul Mattal <paul@archlinux.org>
# Modifier; gripped - forum.odroid.com

pkgname=ffmpeg-neon
_pkgname=ffmpeg
pkgver=2.8.2
pkgrel=2
epoch=1
pkgdesc='Complete and free Internet live audio and video broadcasting solution'
arch=('i686' 'x86_64')
url='http://ffmpeg.org/'
license=('GPL')
depends=(
      'alsa-lib' 'bzip2' 'fontconfig' 'gnutls' 'gsm' 'lame' 'libass' 'libvdpau' 'libwebp'
      'libbluray' 'libmodplug' 'libpulse-neon' 'libssh' 'libsoxr' 'libtheora' 'libva' 'libvidstab.so' 'libvorbis'
      'libvpx-neon' 'libwebp' 'opencore-amr' 'openjpeg' 'opus' 'rtmpdump' 'schroedinger'
       'sdl' 'speex' 'v4l-utils' 'libx264-neon' 'xvidcore' 'zlib' 'x265-neon' 'fribidi' 
      )
makedepends=('hardening-wrapper' 'ladspa' 'yasm')
optdepends=('ladspa: LADSPA filters')
conflicts=('ffmpeg')
provides=('ffmpeg=${pkgver}' 'libavcodec.so' 'libavdevice.so' 'libavfilter.so' 
          'libavformat.so' 'libavresample.so' 'libavutil.so'
          'libpostproc.so' 'libswresample.so'  'libswscale.so')
source=(http://ffmpeg.org/releases/$_pkgname-$pkgver.tar.bz2{,.asc}
        ffmpeg-2.8.1-libvpxenc-remove-some-unused-ctrl-id-mappings.patch)
validpgpkeys=('FCF986EA15E6E293A5644F10B4322F04D67658D8') # ffmpeg-devel
md5sums=('99f31723326dfe1bfd1bc7521338f2d2'
         'SKIP'
         '71be9bcd297cb9ce833bbc5d425c0470')

prepare() {
  cd $_pkgname-$pkgver
  patch -p1 -i ../ffmpeg-2.8.1-libvpxenc-remove-some-unused-ctrl-id-mappings.patch

}

build() {
  cd $_pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --arch="" \
    --cpu=cortex-a15 \
    --enable-pthreads  \
    --disable-debug \
    --enable-neon \
    --enable-nonfree \
    --disable-static \
    --enable-avisynth \
    --enable-avresample \
    --enable-decoder=atrac3 \
    --enable-decoder=atrac3p \
    --enable-dxva2 \
    --enable-fontconfig \
    --enable-gnutls \
    --enable-gpl \
    --enable-ladspa \
    --enable-libass \
    --enable-libbluray \
    --enable-libfreetype \
    --enable-libfribidi \
    --enable-libgsm \
    --enable-libmodplug \
    --enable-libmp3lame \
    --enable-libopencore_amrnb \
    --enable-libopencore_amrwb \
    --enable-libopenjpeg \
    --enable-libopus \
    --enable-libpulse \
    --enable-libschroedinger \
    --enable-libsoxr \
    --enable-libspeex \
    --enable-libssh \
    --enable-libtheora \
    --enable-libv4l2 \
    --enable-libvidstab \
    --enable-libvorbis \
    --enable-libvpx \
    --enable-libwebp \
    --enable-libx264 \
    --enable-libx265 \
    --enable-libxvid \
    --enable-pic \
    --enable-postproc \
    --enable-shared \
    --enable-version3 \
    --enable-x11grab

  make
  make tools/qt-faststart
  export PATH=$PATH:/usr/bin/core_perl #Hack fixme (why is it not in PATH?)
  make doc/ff{mpeg,play,server}.1
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install install-man
  install -D -m755 tools/qt-faststart "$pkgdir"/usr/bin/qt-faststart
}

